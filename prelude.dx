'## Dex prelude

'Runs before every Dex program unless an alternative is provided with `--prelude`.

'Curried wrappers around built-in functions

iadd :: Int -> Int -> Int
iadd x y = %iadd(x, y)

isub :: Int -> Int -> Int
isub x y = %isub(x, y)

imul :: Int -> Int -> Int
imul x y = %imul(x, y)

mod :: Int -> Int -> Int
mod x y = %mod(x, y)

fadd :: Real -> Real -> Real
fadd x y = %fadd(x, y)

fsub :: Real -> Real -> Real
fsub x y = %fsub(x, y)

fmul :: Real -> Real -> Real
fmul x y = %fmul(x, y)

fdiv :: Real -> Real -> Real
fdiv x y = %fdiv(x, y)

ipow :: Int -> Int -> Int
ipow x y = %pow(x, y)

pow :: Real -> Real -> Real
pow x y = %%pow(x, y)

asint :: i -> Int
asint i = %asint(i)

asidx :: Int -> i
asidx i = %asidx(i)

real :: Int -> Real
real x = %inttoreal(x)

b2i :: Bool -> Int
b2i x = %booltoint(x)

filter :: (a -> Bool) -> n=>a -> E m. m=>a
filter f x = %filter(f, x)

-- `todo` is like `undefined` in Haskell
todo :: a
todo = %todo()

'Wrappers around C library functions

exp :: Real -> Real
exp x = %%exp(x)

log :: Real -> Real
log x = %%log(x)

sqrt :: Real -> Real
sqrt x = %%sqrt(x)

sin :: Real -> Real
sin x = %%sin(x)

cos :: Real -> Real
cos x = %%cos(x)

tan :: Real -> Real
tan x = %%tan(x)

floor :: Real -> Int
floor x = %%floorDex(x)

'Misc functions

fst :: (a, b) -> a
fst (x, y) = x

snd :: (a, b) -> b
snd (x, y) = y

b2r :: Bool -> Real
b2r x = real $ b2i x

pi :: Real
pi = 3.141592653589793

ixadd :: i -> Int -> i
ixadd i n = asidx $ iadd (asint i) n

ixsub :: i -> Int -> i
ixsub i n = asidx $ isub (asint i) n

iota :: n=>Int
iota = for i. asint i

dup :: a -> (a, a)
dup x = (x, x)

map :: (a -> b) -> n=>a -> n=>b
map f xs = for i. f xs.i

map2 :: (a -> b) -> n=>m=>a -> n=>m=>b
map2 f xs = for i j. f xs.i.j

zip :: n=>a -> n=>b -> n=>(a,b)
zip xs ys = for i. (xs.i, ys.i)

unzip :: n=>(a, b) -> (n=>a, n=>b)
unzip xys = (map fst xys, map snd xys)

fanout :: A n a. a -> n=>a
fanout x = for i. x

sq :: Real -> Real
sq x = x * x

selectReal :: Bool -> Real -> Real -> Real
selectReal p x y = (x * b2r p) + (y * (1.0 - b2r p))

selectInt :: Bool -> Int -> Int -> Int
selectInt p x y = iadd (imul x (b2i p)) (imul y (isub 1 (b2i p)))

abs :: Real -> Real
abs x = selectReal (x > 0.0) x (0.0 - x)

'State-carrying loop constructs

scan :: a -> n=>(a->(a, b)) -> (a, n=>b)
scan x0 fs = %scan(x0, for i. lam x. fs.i x)

fold :: a -> n=>(a->a) -> a
fold x0 fs = fst $ scan x0 for i. lam x. (fs.i x, ())

-- TODO: call this `scan` and call the current `scan` something else
scan' :: a -> n=>(a->a) -> n=>a
scan' x0 fs = snd $ scan x0 for i. lam x. dup (fs.i x)

'More misc functions

sum :: n=>Real -> Real
sum xs = fold 0.0 for i. x = xs.i; lam c. c + x

isum :: n=>Int -> Int
isum xs = fold 0 for i. x = xs.i; lam c. iadd c x

-- TODO: make this a builtin for efficiency
size :: A n. Int
size = isum for i::n. 1

range :: Int -> E i. ()
range n = %range(n)

mean :: A n. n=>Real -> Real
mean xs = sum xs / real (size @n)

std :: A n. n=>Real -> Real
std xs = sqrt $ mean (map sq xs) - sq (mean xs)

linspace :: A n. Real -> Real -> n=>Real
linspace low high = dx = (high - low) / real (size @n)
                    for i::n. low + real (asint i) * dx

'Functions for working with the pseudorandom number generator

type Key = Int

hash :: Key -> Key -> Key
hash x y = %%threefry2x32(x, y)

splitKey :: Key -> (Key, Key)
splitKey key = (hash key 0, hash key 1)

splitKey3 :: Key -> (Key, Key, Key)
splitKey3 key =
  (k1, key') = splitKey key
  (k2, k3) = splitKey key'
  (k1, k2, k3)

many :: A n a. (Key -> a) -> Key -> n=>a
many f key = for i. f (hash key (asint i))

ixkey :: Key -> n -> Key
ixkey x i = hash x (asint i)

rand :: Key -> Real
rand x = %%randunif(x)

randn :: Key -> Real
randn k =
  (k1, k2) = splitKey k
  u1 = rand k1
  u2 = rand k2
  sqrt (-2.0 * log u1) * cos (2.0 * pi * u2)

randIdx :: A n. Key -> n
randIdx key =
  unif = rand key
  asidx $ floor $ unif * real (size @n)

'Vector spaces and automatic differentiation. Note that many of the following
have implicit vector space typeclass constraints. For now these are not checked
by the type checker.

vzero :: a
vzero = %vzero()

vadd :: a -> a -> a
vadd x y = %vadd(x, y)

vsingle :: n -> a -> n=>a
vsingle i x = %vsingle(i, x)

vsum :: n=>a -> a
vsum xs = %vsum(for i. xs.i)

linearize :: (a -> b) -> a -> (b, a --o b)
linearize f x0 = %linearize(lam x. f x, x0)

jvp :: (a -> b) -> a -> a --o b
jvp f x = llam t. snd (linearize f x) t

linearTranspose :: (a --o b) -> b --o a
linearTranspose f = %linearTranspose(llam t. f t)

vjp :: (a -> b) -> a -> (b, b --o a)
vjp f x = (y, df) = linearize f x
          (y, linearTranspose df)

grad :: (a -> Real) -> a -> a
grad f x = (_, pullback) = vjp f x
           pullback 1.0

fmulDeriv :: Real -> Real -> (Real, Real -> Real -> Real)
fmulDeriv x y = (x * y, lam tx ty. x * ty + y * tx)

vsumImpl :: A n a. n=>a -> a
vsumImpl xs = fold vzero for i::n. lam c. vadd c xs.i

forUnzip :: n=>(b, a -> b) -> (n=>b, a -> n=>b)
forUnzip xfs = (xs, fs) = unzip xfs
               (xs, lam t. for i. fs.i t)
