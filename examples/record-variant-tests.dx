
'Basics:
Syntax for records, variants, and their types.

'Record types

:p {&}
> { &}

:p {a:Int & b:Real}
> {a: Int & b: Real}

:p {a:Int & b:Real &}
> {a: Int & b: Real}

:p {a:Int & a:Real}
> {a: Int & a: Real}


'Records

:p {}
> {,}
:t {}
> { &}

:p {,}
> {,}
:t {,}
> { &}

:p {a=3, b=4}
> {a = 3, b = 4}
:t {a=3, b=4}
> {a: Int & b: Int}

:p {a=3, b=4,}
> {a = 3, b = 4}
:t {a=3, b=4,}
> {a: Int & b: Int}

:p {a=3, a=4}
> {a = 3, a = 4}
:t {a=3, a=4}
> {a: Int & a: Int}

:p
  x = {a=5.0, b=2}
  y : {a:Int & b:Int & ..._} = {a=3, a=4, ...x}
  y
> {a = 3, a = 4, a = 5.0, b = 2}

'Variant (enum) types

:p {|}
> { |}

:p {a:Int | b:Real}
> {a: Int | b: Real}

:p {a:Int | b:Real |}
> {a: Int | b: Real}

:p {a:Int | a:Real}
> {a: Int | a: Real}


'Variants (enums)


:p
  x : {a:Int | b:Real} = {| a=3 |}
  x
> {| a = 3 |}

:p
  x : {a:Int | a:Real | a:Int} = {| a | a = 3.0 |}
  x
> {|a| a = 3.0 |}

:t
  x : {a:Int | a:Real | a:Int} = {| a | a = 3.0 |}
  x
> {a: Int | a: Real | a: Int}

:p
  x : {a:Int | b:Real} = {| a=3 |}
  y : {a:Real | a:Int | ..._} = {| a | ...x |}
  y
> {|a| a = 3 |}


'Parse errors

:p {a:Int & b:Real | c:Int }

> Parse error:95:20:
>    |
> 95 | :p {a:Int & b:Real | c:Int }
>    |                    ^
> unexpected '|'
> expecting "..", "..<", expression, or negation


'Unpacking


:p
  ({b=b, a=a1, a=a2}) = {a=1, b=2, a=3}
  (a1, a2, b)
> (1, (3, 2))

({b=b, a=a1, a=a2}) = {a=1, b=2, a=3}
:p (a1, a2, b)
> (1, (3, 2))

:p
  ({b=b, a=a1, a=a2}) = {a=1, b=2}
  (a1, a2, b)
> Type error:Labels in record pattern do not match record type. Expected structure {a: Int & b: Int}
>
>   ({b=b, a=a1, a=a2}) = {a=1, b=2}
>    ^^^^^^^^^^^^^^^^^

:p
  x : {a:Int | a:Real | a:Int} = {| a | a = 3.0 |}
  ({| a | a = a |}) = x
  x
> Type error:Variant not allowed in can't-fail pattern
>
>   ({| a | a = a |}) = x
>    ^^^^^^^^^^^^^^^

x : {a:Int | a:Real | a:Int} = {| a | a = 3.0 |}
({| a | a = a |}) = x
> Type error:Variant not allowed in can't-fail pattern


'Pattern matching

-- Not allowed: use a let binding instead
:p case {a=1, b=2, a=3} of
  {b=b, a=a1, a=a2} -> (a1, a2, b)
> Type error:Case patterns must start with a data constructor or variant pattern
>
>   {b=b, a=a1, a=a2} -> (a1, a2, b)
>   ^^^^^^^^^^^^^^^^^^

:p
  x : {a:Int | a:Real | b:Int} = {| a | a = 3.0 |}
  case x of
    {| a = x |} -> i2r x
    {| a | a = x |} -> x
    {| b = x |} -> i2r x
> 3.0

'Table values and imp lowering

myRecordTable : (Fin 2)=>{a:Int & b:Real} =
  [{a=1, b=2.0}, {a=3, b=4.0}]

:p myRecordTable
> [{a = 1, b = 2.0}, {a = 3, b = 4.0}]

:p for i:(Fin 2).
    ({a=a, b=b}) = myRecordTable.i
    {a=b, b=a}
> [{a = 2.0, b = 1}, {a = 4.0, b = 3}]

myVariantTable = [{| a=1 |}, {| b=2.0 |}]: (Fin 2)=>{a:Int | b:Real}
:p myVariantTable
> [{| a = 1 |}, {| b = 2.0 |}]

:p for i:(Fin 2).
    v : {a:_ | b:_} = case myVariantTable.i of
      {| a=a |} -> {| b=a |}
      {| b=b |} -> {| a=b |}
    v
> [{| b = 1 |}, {| a = 2.0 |}]

'As index sets

:p size {a:Fin 10 & b:Fin 3}
> 30
:p ordinal {a=(7@Fin 10), b=(2@Fin 3)}
> 27
:p fromOrdinal {a:Fin 10 & b:Fin 3} 14
> {a = 4@Fin 10, b = 1@Fin 3}

recordsAsIndices : {a:Fin 2 & b:Fin 3}=>{a:Fin 2 & b:Fin 3} = for i. i

-- TODO: error when printing a table indexed by records

:p size {a:Fin 10 | b:Fin 3}
> 13

:p
  x : {a:Fin 10 | b:Fin 3} = {| b=(2@_) |}
  ordinal x
> 12

:p fromOrdinal {a:Fin 10 | b:Fin 3} 4
> {| a = 4@Fin 10 |}

:p fromOrdinal {a:Fin 10 | b:Fin 3} 11
> {| b = 1@Fin 3 |}

variantsAsIndices : {a:Fin 10 | b:Fin 3}=>{a:Fin 10 | b:Fin 3} = for i. i

-- TODO: error when printing a table indexed by variants
