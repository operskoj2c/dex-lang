
include "examples/diagram.dx"

data CompactSet a:Type =
  Interval a a
  Singleton a

def Scale (a:Type) : Type =
  { mapping : a -> Maybe Float
  & range   : List (CompactSet Float) }  -- non-overlapping, ordered

def ScaledData (n:Type) (a:Type) : Type =
  { scale : Scale a
  & dat   : n => a }

def Plot (n:Type) (xdata:Type) (ydata:Type) : Type =
  { xs : ScaledData n xdata
  & ys : ScaledData n ydata }

def applyScale (s:Scale a) (x:a) : Maybe Float = getAt #mapping s x

unitTypeScale : Scale Unit = { mapping = \(). Just 0.0
                             , range   = AsList _ [Singleton 0.0] }

def projectUnitInterval (x:Float) : Maybe Float =
  case x >= 0.0 && x <= 1.0 of
    True  -> Just x
    False -> Nothing

unitIntervalScale : Scale Float =
  { mapping = projectUnitInterval
  , range   = AsList _ [Interval 0.0 1.0] }

def mapScale (s:Scale a) (f: b -> a) : Scale b =
  { mapping = \x. getAt #mapping s (f x)
  , range = getAt #range s }

def floatScale (xmin:Float) (xmax:Float) : Scale Float =
  mapScale unitIntervalScale (\x. (x - xmin) / (xmax - xmin))

def getScaled (sd:ScaledData n a) (i:n) : Maybe Float =
  applyScale (getAt #scale sd) (getAt #dat sd).i

def showPlot (plot:Plot n a b) : String =
  points = concatDiagrams for i.
    x = getScaled (getAt #xs plot) i
    y = getScaled (getAt #ys plot) i
    case x of
      Just x' -> case y of
        Just y' -> dot |> moveXY (x', y') |> setStroke blue
        Nothing -> mempty
      Nothing -> mempty
  boundingBox = moveXY (0.5, 0.5) $ rect 1.0 1.0
  diagram = boundingBox <> points
  renderSVG diagram ((0.0, 0.0), (1.0, 1.0))


blankData : ScaledData n Unit = {scale = unitTypeScale, dat = for i. ()}

blankPlot : Plot n Unit Unit = { xs = blankData
                               , ys = blankData }

-- -- TODO: generalize beyond Float with a type class for auto scaling
def autoScale (xs:n=>Float) : ScaledData n Float =
  {scale = floatScale (minimum xs) (maximum xs), dat = xs}

def setXDataScaled (xs:ScaledData n c) (plot:Plot n a b) : Plot n c b =
  -- We can't use `setAt` here because we're changing the type
  {xs = xs, ys = getAt #ys plot}

def setYDataScaled (ys:ScaledData n c) (plot:Plot n a b) : Plot n a c =
  {xs = getAt #xs plot, ys = ys}

def xyPlot (xs:n=>Float) (ys:n=>Float) : Plot n Float Float =
  blankPlot |> setXDataScaled (autoScale xs) |> setYDataScaled (autoScale ys)
